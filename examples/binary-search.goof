include "std.goof"

macro BUFFER_CAP 256 end

macro cnt mem end
macro key cnt 8 + end
macro nums key 8 + end

cnt
BUFFER_CAP over stdin read drop // read cnt

dup stoi     // mem[cnt] cnt
.64
cnt ,64      // cnt

0 
// cnt i
while 2dup > do
    dup 8 * nums +
    BUFFER_CAP over stdin read drop
    dup stoi .64
    1 + // i++
end

drop

key
BUFFER_CAP over stdin read drop // read key

dup stoi
.64

1 - 
0 

// hi lo
while 2dup 2dup > rot = | do
    2dup + 1 shr // hi lo mid
    dup 8 * nums + ,64 // hi lo mid nums[mid]
    
    dup key ,64 = if
        drop
        dump
        0 exit
    else
        // key > nums[mid]
        dup key ,64 < if
            // update lo 
            drop
            swap drop
            1 +
            // hi mid+1
        else
            // update hi
            drop
            rot rot
            drop
            1 -
            swap
            // mid-1 lo
        end
    end
end

drop drop
1 exit
